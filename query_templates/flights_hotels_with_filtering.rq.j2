PREFIX sp: <http://spinrdf.org/sp#>
PREFIX cp: <http://schemas.openxmlformats.org/package/2006/metadata/core-properties/>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX fx: <http://sparql.xyz/facade-x/ns/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX xhtml: <http://www.w3.org/1999/xhtml#>
PREFIX spec: <https://html.spec.whatwg.org/#>
PREFIX ns: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX fa: <https://w3id.org/sparql-anything#>

SELECT DISTINCT (?city as ?destinationCity) (?depTimeText as ?flightDepartureTime) (?name as ?hotelName) (?price as ?hotelPrice) ?hotelPricePerDay (?duration as ?stayDuration) (?standard as ?hotelStandard) 
WHERE {
  SERVICE <x-sparql-anything:> {
    fx:properties fx:location "https://www.tui.pl/api/services/tui-search/api/search/offers";
    fx:http.payload {{ payload | tojson }};
    fx:http.method "POST" ;
    fx:media-type "application/json" ;
    fx:http.header.Content-Type "application/json" ;
    fx:http.header.Accept "application/json" .
    
    ?root rdf:type fx:root .
    ?root xyz:offers ?a .
	?a ?any ?b .
    ?b xyz:hotelName ?name ;
       xyz:discountPerPersonPrice ?price ;
       xyz:duration ?duration ;
       xyz:hotelStandard ?standard ;
       xyz:city ?city ;
  }
  SERVICE <x-sparql-anything:> {
    fx:properties fx:location "https://www.modlinairport.pl/pasazer/rozklad-lotow" .
    fx:properties fx:media-type "text/html" .
    ?node ns:type xhtml:table .
    ?node xhtml:class ?class .
    FILTER(CONTAINS(?class, "departures")) .
    ?node (ns:_1|ns:_2|ns:_3|ns:_4|ns:_5|ns:_6|ns:_7|ns:_8|ns:_9|ns:_10|ns:_11|ns:_12|ns:_13|ns:_14)+ ?child .
    ?child ns:type xhtml:td .
    ?child spec:innerText ?childText .
    ?parent ns:_1|ns:_2|ns:_3|ns:_4|ns:_5|ns:_6|ns:_7|ns:_8|ns:_9|ns:_10|ns:_11|ns:_12|ns:_13|ns:_14 ?child .
    ?parent spec:innerHTML ?inner .
    ?parent ns:_1 ?flight .
    ?flight spec:innerText ?flightId .
    ?parent ns:_2 ?dest .
    ?dest spec:innerText ?destText .
    ?parent ns:_3 ?depTime .
    ?depTime spec:innerText ?depTimeText .
    OPTIONAL {
      ?parent ns:_4 ?state .
      ?state spec:innerText ?stateText .
    }
  }
  
  FILTER (CONTAINS(lcase(STR(?destText)), lcase(STR(?city))))
  BIND(xsd:integer(?price) AS ?intPrice)
  BIND(xsd:integer(?intPrice/?duration) as ?hotelPricePerDay)

  FILTER(?hotelPricePerDay < {{ max_price_per_day }})
  FILTER(?duration = {{ duration }})
  FILTER(?standard >= {{ min_standard }})
}
ORDER BY ?hotelPricePerDay
